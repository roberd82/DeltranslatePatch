if (instance_number(obj_gamecontroller) > 1)
{
    instance_destroy();
    exit;
}

is_connecting_controller = 3;
gamepad_active = 0;
gamepad_id = 0;
gamepad_shoulderlb_reassign = 0;
gamepad_type = "";

global.is_console = scr_is_switch_os() || os_type == os_ps4 || os_type == os_ps5;

global.lang_folder = working_directory + "lang/"

if (!variable_global_exists("gamepad_type"))
    global.gamepad_type = "N/A";

if (variable_global_exists("lang_map"))
    return;

ossafe_ini_open("true_config.ini")
global.special_mode = ini_read_real("LANG", "special_mode", 0)
global.translated_songs = ini_read_real("LANG", "translated_songs", 1)
ossafe_ini_close()
global.lang_sprites = ds_map_create()
global.lang_sounds = ds_map_create()
global.font_map = ds_map_create()
global.langs_names = []
global.lang_settings = {}
global.languages_list = []
global.lang = "en"
lang_changes_call = -1
lang_changes = {}
cur_translation_version = [0, 0, 0]
last_translation_version = [0, 0, 0]
translation_version_changes_files = []
translation_version_changes_datas = []
datas_loading = {}
translation_version_description = ""
translation_external_update = false

is_valid_version = function(str) {
    if (is_undefined(str))
        str = "";

    var major_pos = string_pos_ext(".", str, 1)
    var minor_pos = string_pos_ext(".", str, major_pos + 1)
    
    return major_pos != 0 && minor_pos != 0
}

string_to_version = function(str) {
    if (is_undefined(str))
        str = "0.0.0";

    try {
        var ver = [0, 0, 0]

        var major_pos = string_pos_ext(".", str, 1)
        var minor_pos = string_pos_ext(".", str, major_pos + 1)
        
        ver[0] = real(string_copy(str,             1,                      major_pos));
        ver[1] = real(string_copy(str, major_pos + 1,          minor_pos - major_pos));
        ver[2] = real(string_copy(str, minor_pos + 1, string_length(str) - minor_pos));
    } catch (err) {
        return [-1, -1, -1]
    }

    return ver
}

version_to_string = function(ver) {
    if (is_undefined(ver))
        ver = [0, 0, 0];

    return string(ver[0]) + "." + string(ver[1]) + "." + string(ver[2]);
}

is_version_greater = function(ver1, ver2) {
    if (is_undefined(ver1))
        ver1 = [0, 0, 0];
    if (is_undefined(ver2))
        ver2 = [0, 0, 0];

    return ver1[0] > ver2[0] ||
    (ver1[0] == ver2[0] && ver1[1] > ver2[1]) ||
    (ver1[0] == ver2[0] && ver1[1] == ver2[1] && ver1[2] > ver2[2])
}

update_lang_version = function(lang) {
    var version = string_to_version("0.0.0")
    var changes_file = get_lang_folder_path() + "changes.json"
    if (file_exists(changes_file)) {
        var changes = scr_load_json(changes_file)
        var versions = variable_struct_get_names(changes)

        for (var i = 0; i < array_length(versions); i++) {
            var ver = string_to_version(versions[i])

            if (is_version_greater(ver, version)) {
                version = ver;
            }
        }
    }

    cur_translation_version = version;
    last_translation_version = version;
}

update_language = function() {
    if (file_exists(global.lang_folder + "settings.json")) {
        var settings = scr_load_json(global.lang_folder + "settings.json")

        var lang_code = variable_struct_get(settings, "lang_code")
        if (is_undefined(lang_code))
            lang_code = "en"

        global.lang = lang_code
        global.lang_settings = settings

        update_lang_version()
    } else {
        global.lang_settings = json_parse("{\"name\": \"English\"}");
    }
}

update_language()

var url = get_lang_setting("files_url", "")

if (url != "") {
    lang_changes_call = http_get(url + "changes.json")
}


scr_init_localization()

_alpha = 0;

desc_folded = true;
panel_visible = true;

loading_new_translation_files = false

files_in_upload = {}
datas_in_upload = {}
loaded_files = []
loaded_datas = []
loading_error = ""

load_files = function() {
    files_in_upload = {}
    loaded_files = []
    loading_error = ""

    var files = translation_version_changes_files;
    for (var i = 0; i < array_length(files); i++) {
        var file = string_replace_all(files[i], "..", "")
        variable_struct_set(files_in_upload, file, http_get_file(get_lang_setting("files_url", "") + files[i], "\\\\?\\" + program_directory + "tmp/" + file));
    }
    if (!variable_struct_exists(files_in_upload, "settings.json")) {
        variable_struct_set(files_in_upload, "settings.json", http_get_file(get_lang_setting("files_url", "") + "settings.json", "\\\\?\\" + program_directory + "tmp/settings.json"));
    }
    if (!variable_struct_exists(files_in_upload, "changes.json")) {
        variable_struct_set(files_in_upload, "changes.json", http_get_file(get_lang_setting("files_url", "") + "changes.json", "\\\\?\\" + program_directory + "tmp/changes.json"));
    }
}

load_datas = function() {
    datas_in_upload = {}
    loaded_datas = []
    loading_error = ""

    var datas = translation_version_changes_datas;
    for (var i = 0; i < array_length(datas); i++) {
        var urls = get_lang_setting("datas_url", [])
        if (datas[i] < array_length(urls)) {
            var file = urls[datas[i]]
            var path = ""
            if (datas[i] > 0) {
                path = "chapter" + string(datas[i])
            }
            variable_struct_set(datas_in_upload, datas[i], http_get_file(file, "\\\\?\\" + program_directory + "tmp/" + path + "/data.win"));
        } else {
            show_message(string("Index '{0}' out of range of 'datas_url'", string(datas[i])))
        }
    }
}

copy_files_from_tmp = function() {
    for (var i = 0; i < array_length(loaded_files); i++) {
        file_copy(
            "\\\\?\\" + program_directory + "tmp/" + loaded_files[i],
            "\\\\?\\" + program_directory + "lang/" + loaded_files[i],
        )
    }

    for (var i = 0; i < array_length(loaded_datas); i++) {
        var path_from = "data.win"
        var path_to = "data.win"
        if (loaded_datas[i] > 0) {
            path_from = "chapter" + string(loaded_datas[i]) + "/data.win"
            path_to = "chapter" + string(loaded_datas[i]) + "_windows/data.win"
        }
        file_copy(
            "\\\\?\\" + program_directory + "tmp/" + path_from,
            "\\\\?\\" + program_directory + path_to,
        )

    }

    update_language()

    loading_new_translation_files = false
    scr_init_localization()

    directory_destroy("\\\\?\\" + program_directory + "tmp")
}

clear_tmp = function() {
    directory_destroy("\\\\?\\" + program_directory + "tmp")
}


